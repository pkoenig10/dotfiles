#!/usr/bin/env fish

function cmd
    set -l color_gray (set_color brblack)
    set -l color_normal (set_color normal)

    echo -s $color_gray "> $argv" $color_normal

    $argv
end

function brew_bundle -a upgrade -a verbose
    echo "Installing Homebrew packages"

    set -l options
    if test -z $upgrade
        set -a options --no-upgrade
    end
    if test -n $verbose
        set -a options --verbose
    end

    cmd brew bundle $options

    echo
end

function link -a path
    echo "Linking ~/$path"

    cmd mkdir -p (dirname ~/$path)
    cmd rm -rf ~/$path
    cmd ln -s $dir/$path ~/$path

    echo
end

argparse u/upgrade v/verbose -- $argv

git submodule update --init --recursive

set dir (realpath (status dirname))

if command -q brew
    brew_bundle "$_flag_upgrade" "$_flag_verbose"
end

link .config/fish/config.fish
link .config/fish/functions
link .gitconfig
link .ssh/config
link .vim/pack
link .vimrc
