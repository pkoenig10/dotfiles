#!/usr/bin/env fish

git submodule update --init --recursive

set dir (realpath (status dirname))

function cmd
    set -l color_gray (set_color brblack)
    set -l color_normal (set_color normal)

    echo -s $color_gray "> $argv" $color_normal

    $argv
end

function brew_install
    echo "Installing Homebrew packages"

    cmd brew install -q \
        android-platform-tools \
        bluesnooze \
        fd \
        fish \
        git \
        orbstack \
        rectangle \
        ripgrep \
        sd \
        sourcetree \
        the-unarchiver \
        vim \
        visual-studio-code

    echo
end

function link -a path
    echo "Linking ~/$path"

    cmd mkdir -p (dirname ~/$path)
    cmd rm -rf ~/$path
    cmd ln -s $dir/$path ~/$path

    echo
end

if command -q brew
    brew_install
end

link .config/fish/config.fish
link .config/fish/functions
link .gitconfig
link .ssh/authorized_keys
link .ssh/config
link .vim
link .vimrc
