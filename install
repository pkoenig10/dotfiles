#!/usr/bin/env fish

git submodule update --init --recursive

set dir (realpath (status dirname))

function cmd
    set -l color_bold (set_color --bold)
    set -l color_normal (set_color normal)

    echo -s $color_bold "> $argv" $color_normal

    $argv
end

function brew_install
    echo "Installing Homebrew packages"

    cmd brew install -q \
        fish \
        git \
        ripgrep \
        vim

    echo
end

function link -a path
    echo "Linking ~/$path"

    cmd rm -rf ~/$path
    cmd ln -s $dir/$path ~/$path

    echo
end

if command -q brew
    brew_install
end

link .config/fish/config.fish
link .config/fish/functions
link .gitconfig
link .ssh/config
link .vim
link .vimrc
